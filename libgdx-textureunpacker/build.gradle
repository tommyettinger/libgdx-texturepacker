[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

import proguard.gradle.ProGuardTask

apply plugin: 'application'

java.sourceCompatibility = 1.8
mainClassName = 'com.badlogic.gdx.tools.texturepacker.TextureUnpacker'
eclipse.project.name = appName + '-lwjgl3'
sourceSets.main.resources.srcDirs = [file('../assets').path]

dependencies {
	//noinspection GDXOutdatedVersionGradleGroovy
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
}

tasks.register('fatJar', org.gradle.jvm.tasks.Jar) {
	dependsOn classes
	archiveFileName.set("${project.name}-fat.jar")
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	dependsOn configurations.runtimeClasspath
	from files(sourceSets.main.output.getClassesDirs())
	from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
	dependencies {
		exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
	}
	manifest {
		attributes 'Main-Class': project.mainClassName
	}
	doLast {
		file(archiveFile).setExecutable(true, false)
	}
}

tasks.register('proguard', ProGuardTask) {
//	(configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } + file("build/libs/${appName}.jar")).each { println(it) }
	outputs.upToDateWhen { false }
	configuration 'proguard-rules.pro'
	//printmapping 'build/lib/out.map'
	if (System.getProperty('java.version').startsWith('1.')) {
		libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
	} else {
		libraryjars "${System.getProperty('java.home')}/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
	}
	injars(file("build/libs/${project.name}-fat.jar"))
	outjars "build/libs/${project.name}-runnable-${version}.jar"
}

proguard.dependsOn fatJar
